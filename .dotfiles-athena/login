#
# .login - based on Athena
# chad - yandros@MIT.EDU

### Environment Settings
if (-r ~/.environment) then
	source ~/.environment
endif


### lockers
if (-r ~/.lockers) then
  source ~/.lockers
endif


### Path setting
if (-r ~/lib/@sys/path) then
        source ~/lib/@sys/path
else if (-r ~/.path) then
            source ~/.path
     else
	    set path = ( /bin/athena /usr/athena/bin /bin /usr/bin )
	    echo "Dead path."
     endif	
endif


### TERM settings
if (("$term" == switch) || ("$term" == network) || ("$term" == dialup)) then
    if (! $?DEF_TERM) setenv DEF_TERM vt100
    set noglob; eval `tset -s -I -Q "?${DEF_TERM}"` || unset noglob
endif


### DISPLAY setting
if (("$term" =~ xterm*) && (! $?DISPLAY)) then
	echo -n "What DISPLAY are you using [default: none]? "
	set response = ($<)
	if ("$response" != "") then
		if ($response =~ *:*) then
			echo "Setting DISPLAY to $response"
			setenv DISPLAY "$response"
		else
			echo "Setting DISPLAY to ${response}:0"
			setenv DISPLAY "${response}:0"
		endif
	else
		echo "No DISPLAY set..."
	endif
endif
